################################################################################
### Part 0) Downloading part
################################################################################

rule download_genomes:
    output:
        "../data/genomes/{species}_genomes.fna"
    shell:
        "bash ../scripts/download_genomes.sh {wildcards.species} {output}"

rule download_features:
    output:
        "../data/factors/features/{species}_feature_table.txt"
    shell:
        "bash ../scripts/download_features.sh {wildcards.species} {output}"

rule download_repeats:
    output:
        "../data/factors/repeats/{species}_repeats.txt"
    shell:
        "bash ../scripts/download_repeats.sh {wildcards.species} {output}"

rule download_rr:
    output:
        "../data/factors/recombination_rates/{species}_RR_spline.RData"
    shell:
        "bash ../scripts/download_repeats.sh {wildcards.species} {output}"

rule sampling:
    input:
        "../data/genomes/{species}_genomes.fna"
    output:
        "../data/samples/{windows}_{n_samples}/{species}_sample.fna"
    shell:
        "python3 ../scripts/sampling_windows.py {input} {wildcards.n_samples} \
            {wildcards.windows} scaling {output}"

################################################################################
### Part A) From whole genome to the wanted figure
################################################################################

rule CGR:
    input:
        "../data/samples/{windows}_{n_samples}/{species}_sample.fna"
    output:
        "../data/following/CGRs/{windows}_{n_samples}/{species}_done.txt"
    threads:
        99
    shell:
        "python3 ../scripts/windowed_CGR.py {input} {wildcards.windows} \
            {wildcards.n_samples} {threads} {output}"

rule factor_extract:
    input:
        "../data/genomes/{species}_genomes.fna",
        "../data/samples/{windows}_{n_samples}/{species}_sample.fna",
        "../data/factors/repeats/{species}_repeats.txt",
        "../data/factors/features/{species}_feature_table.txt",
        "../data/factors/recombination_rates/{species}_RR_spline.RData"
    output:
        "files/factors/{windows}_{n_samples}/{species}_{factors}.txt"
    shell:
        "bash scripts/factor_type.sh {wildcards.factors} {wildcards.windows} \
            {input} {output}"

rule ratio_extract:
    input:
        "../data/following/CGRs/{windows}_{n_samples}/{species}_done.txt"
    output:
        "files/ratios/{windows}_{n_samples}/{species}_ratios.txt"
    shell:
        "python3 scripts/extract_nucleotide_ratios.py {input} \
            {wildcards.species} {wildcards.windows} {wildcards.n_samples} \
            {threads} {output}"

rule FCGR:
    input:
        "../data/following/CGRs/{windows}_{n_samples}/{species}_done.txt"
    output:
        "files/FCGRs/{windows}_{n_samples}_{kmer}/{species}_FCGRs.txt"
    threads:
        99
    shell:
        "python3 ../scripts/windowed_FCGR.py {input} {wildcards.species} \
            {wildcards.windows} {wildcards.kmer} {wildcards.n_samples} \
            {threads} {output}"

rule FCGR_dist:
    input:
        "files/FCGRs/{windows}_{n_samples}_{kmer}/{species}_FCGRs.txt"
    output:
        "files/distances/manhattan/{windows}_{n_samples}_{kmer}/{species}_dist_matrix.RData"
    threads:
        99
    shell:
        "Rscript ../scripts/manhattan_distance.R {input} {output}"

rule fitting:
    input:
        "files/distances/manhattan/{windows}_{n_samples}_{kmer}/{species}_dist_matrix.RData"
    output:
        "files/distances/manhattan/{windows}_{n_samples}_{kmer}/{species}_fit.RData"
    threads:
        99
    shell:
        "Rscript ../scripts/fitting_dist.R {input} {output}"

rule correlation:
    input:
        "files/distances/manhattan/{windows}_{n_samples}_{kmer}/{species}_dist_matrix.RData",
        "files/factors/{windows}_{n_samples}/{species}_{factors}.txt"
    output:
        "files/results/{windows}_{n_samples}_{kmer}_{factors}/{species}_correlation.png"
    shell:
        "Rscript scripts/cor_dist_factor.R {output} {input} FCGRs \
            {wildcards.factors} {wildcards.windows} {wildcards.n_samples} \
            {wildcards.species} {wildcards.kmer}"

rule MDS:
    input:
        "files/distances/manhattan/{windows}_{n_samples}_{kmer}/{species}_fit.RData",
        "files/factors/{windows}_{n_samples}/{species}_{factors}.txt"
    output:
        "files/results/{windows}_{n_samples}_{kmer}_{factors}/{species}_MDS.png"
    shell:
        "Rscript scripts/MDS_dist_factor.R {output} {input} FCGRs \
            {wildcards.factors} {wildcards.windows} {wildcards.n_samples} \
            {wildcards.species} {wildcards.kmer}"

rule ratio_figure:
    input:
        "files/distances/manhattan/{windows}_{n_samples}_{kmer}/{species}_dist_matrix.RData",
        "files/ratios/{windows}_{n_samples}/{species}_ratios.txt"
    output:
        "files/results/ratios/{windows}_{n_samples}_{kmer}/{species}.png"
    shell:
        "Rscript scripts/nucleotide_ratio_figures.R {output} {input} \
            {wildcards.windows} FCGRs {wildcards.kmer}"
